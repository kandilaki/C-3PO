apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'application'

sourceCompatibility = JavaVersion.VERSION_11
version = '0.3-SNAPSHOT'

mainClassName = "org.c_3po.Main"

jar {
    manifest {
        attributes 'Implementation-Title': 'C-3PO', 'Implementation-Version': version, 'Main-Class': 'org.c_3po.Main'
    }
}

test {
    // This enables JUnit 5. Otherwise gradle would not find the tests
    // and Spock as of 2.0 is based on JUnit Platform (i.e. JUnit 5)
    useJUnitPlatform()
}

repositories {
    mavenCentral()
}

dependencies {

    // Thymeleaf
    implementation group: 'org.thymeleaf', name: 'thymeleaf', version: '2.1.6.RELEASE'
    implementation group: 'nz.net.ultraq.thymeleaf', name: 'thymeleaf-layout-dialect', version: '1.4.+'

    // Explicitly require Groovy 3 although Thymeleaf layout dialect requires
    // Groovy 2 already. Reason: to be compatible with Java 17 (otherwise
    // reflection exceptions would be thrown when executing c-3po)
    implementation group: 'org.codehaus.groovy', name: 'groovy', version: '3.0.+'

    // To support LEGACYHTML5 Thymeleaf mode
    // Note: nekohtml is rather old and no new releases are expected
    implementation group: 'net.sourceforge.nekohtml', name: 'nekohtml', version: '1.9.22'

    // Logging
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.+'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.+'

    // Markdown support
    // Note: Has been moved to org.commonmark since 0.17.0,
    // see https://github.com/commonmark/commonmark-java/issues/187.
    // We can't depend on any greater than 0.6.0 because 0.7.0 obviously introduced a
    // breaking API change (i.e. no HtmlRenderer class)
    implementation group: 'com.atlassian.commonmark', name: 'commonmark', version: '0.6.0'

    // SCSS and SASS support
    // Note: 5.10.+ throws some SASS deprecation warnings that are not
    // understood at time of writing.
    implementation group: 'io.bit3', name: 'jsass', version: '5.9.+'

    // For asset fingerprinting to parse HTML
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.16.+'

    // Testing
    testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.3-groovy-3.0'
}

installDist.dependsOn test
